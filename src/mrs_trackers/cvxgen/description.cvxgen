# Produced by CVXGEN, 2017-12-28 05:21:08 -0500.
# CVXGEN is Copyright (C) 2006-2017 Jacob Mattingley, jem@cvxgen.com.
# The code in this file is Copyright (C) 2006-2017 Jacob Mattingley.
# CVXGEN, or solvers produced by CVXGEN, cannot be used for commercial
# applications without prior written permission from Jacob Mattingley.

# Filename: description.cvxgen.
# Description: A description of the CVXGEN problem.

dimensions
  m = 1  # inputs.
  n = 3  # states.
  T = 40  # horizon.
end

parameters
  A (n,n)  {1,1 2,2 3,3 1,2 2,3}  # dynamics matrix.
  B (n,m)  {3,1} # transfer matrix.
  Af (n,n) {1,1 2,2 3,3 1,2 2,3} # dynamics matrix for first prediction.
  Bf (n,m) {3,1} # transfer matrix for first prediction.

  Q (n,n) psd    # state cost.
  R psd symmetric nonnegative  # input cost.

  x[0] (n) # initial state.
  x_max_2 nonnegative
  x_max_3 nonnegative
  x_min_2
  x_min_3

  x_ss[t] (n), t=1..T
end

variables
  x[t] (n), t=1..T  # state.
  u[t] (m), t=0..T  # input.
end

minimize
  sum[t=1..T](quad(x[t]-x_ss[t], Q)) + sum[t=1..T](quad(u[t], R))
subject to
  x[1] == Af*x[0] + Bf*u[0]
  x[t+1] == A*x[t] + B*u[t], t=1..T-1 # dynamics constraints

  x[t][2] <= x_max_2, t=1..T
  x[t][3] <= x_max_3, t=1..T
  x[t][2] >= x_min_2, t=1..T
  x[t][3] >= x_min_3, t=1..T  #state constraints

end
